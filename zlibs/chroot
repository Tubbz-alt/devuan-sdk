#!/usr/bin/env zsh

# Devuan SDK - chroot management

# Copyright (C) 2015 Dyne.org Foundation
#
# Devuan SDK is designed, written and maintained by Denis Roio <jaromil@dyne.org>
#
# This source code is free software; you can redistribute it and/or
# modify it under the terms of the GNU Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This source code is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  Please refer
# to the GNU Public License for more details.
#
# You should have received a copy of the GNU Public License along with
# this source code; if not, write to: Free Software Foundation, Inc.,
# 675 Mass Ave, Cambridge, MA 02139, USA.

chroot arch() {
    fn arch

    case $1 in
        i386|amd64) chroot_arch=$1 ;;
        *)
            warn "Chroot architecture not supported $1"
            act -n "Existing chroots: "
            schroot -l 2>/dev/null | awk -F: '
/devuan/ {
  gsub(/devuan-/,"",$2)
  printf $2 " "
}'
            print >&2
            return 1
            ;;
    esac

    notice "Chroot selected: $chroot_arch"
    if [[ -r $sdk_path/chroot/$chroot_arch/etc/passwd ]]; then
        act "ready for use (build or run chroot-enter)"
    else
        act "not existing yet (run chroot-create)"
    fi
}

chroot-config() {
    fn chroot-config
    req=(chroot_arch)
    reqck

    [[ -r /etc/schroot/chroot.d/devuan-$chroot_arch ]] && {
        warn "Chroot configuration already existing: /etc/schroot/chroot.d/devuan-$chroot_arch"
        warn "Delete it and eventually erase chroot/$chroot_arch to rewrite"
        return 1
    }

    mkdir -p $sdk_path/chroot/$chroot_arch

    tmp=`mktemp`
    chmod a+r $tmp
    cat <<EOF >> $tmp
[devuan-$chroot_arch]
type=directory
directory=$sdk_path/chroot/$chroot_arch
description=Devuan SDK $chroot_arch
users=$USER
root-users=$USER
aliases=${release}-${chroot_arch}
EOF

    sudo \
        cp $tmp /etc/schroot/chroot.d/devuan-$chroot_arch
    res=$?

    rm $tmp

    return $res
}

chroot-create() {
    fn chroot-create
    req=(chroot_arch)
    freq=(chroot/$chroot_arch)
    reqck

    [[ -r $sdk_path/chroot/$chroot_arch/usr/bin/zsh ]] && {
        warn "Chroot already existing: $sdk_path/chroot/$chroot_arch"
        return 0
    }

    notice "First stage debootstrap"

    sudo \
        debootstrap --components=$chroot_components \
        --variant=$chroot_variant \
        --verbose \
        --foreign --arch $chroot_arch \
        --include=$chroot_include \
        --exclude=$chroot_exclude \
        --no-check-gpg \
        $release $sdk_path/chroot/$chroot_arch $mirror

    notice "Second stage debootstrap"

    schroot -c devuan-$chroot_arch -u root \
        -d /debootstrap -- ./debootstrap --second-stage

    act "Adding apt sources"
    sudo \
        rsync -a $sdk_path/config.d/* \
        $sdk_path/chroot/$chroot_arch/

    act "Updating package lists"
    schroot -c devuan-$chroot_arch -u root \
        apt-get update
}

chroot-enter() {
    fn chroot-enter
    req=(chroot_arch)
    freq=(chroot/$chroot_arch/etc/passwd)
    reqck

    
    if [[ -r $sdk_path/chroot/$chroot_arch/bin/zsh ]]; then
        chroot_shell=/bin/zsh
    else
        warn "Zsh not found in chroot $chroot_arch"
        chroot_shell=/bin/bash
    fi
        
    schroot -c devuan-$chroot_arch -u root -d /root -s $chroot_shell

}

